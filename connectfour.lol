OBTW
	Connect Four LOLCODE implementation.
TLDR

HAI 1.4

	CAN HAS STRING?
	CAN HAS SOCKS?

	O HAI IM BORD
		I HAS A WIDTH
		I HAS A HEIGHT

		I HAS A NUN ITZ 0
		I HAS A RED ITZ 1
		I HAS A YELO ITZ 2
		I HAS A TRUN ITZ RED

		HOW IZ I PARSIN YR x AN YR y
			FOUND YR MAEK SMOOSH x y MKAY A YARN
		IF U SAY SO

		HOW IZ I GETTIN YR x AN YR y
			I HAS A LOC
			LOC R I IZ ME'Z PARSIN YR x AN YR y MKAY
			FOUND YR ME'Z SRS LOC
		IF U SAY SO

		HOW IZ I REVILIN YR x AN YR y
			I HAS A VAL
			VAL R I IZ ME'Z GETTIN YR x AN YR y MKAY
			BOTH SAEM VAL AN ME'Z RED, O RLY?
				YA RLY, FOUND YR "R"
				MEBBE BOTH SAEM VAL AN ME'Z YELO, FOUND YR "Y"
				NO WAI, FOUND YR "."
			OIC
		IF U SAY SO

		HOW IZ I SETTIN YR x AN YR y AN YR VAL
			I HAS A LOC
			LOC R I IZ ME'Z PARSIN YR x AN YR y MKAY
			ME'Z SRS LOC R VAL
			FOUND YR VAL
		IF U SAY SO

		HOW IZ I TRUNSWAP
			BOTH SAEM ME'Z TRUN AN ME'Z RED
			O RLY?
				YA RLY
					ME'Z TRUN R ME'Z YELO
				NO WAI
					ME'Z TRUN R ME'Z RED
			OIC
		IF U SAY SO

		HOW IZ I PRINTIN

			VISIBLE ""

			IM IN YR OUTR UPPIN YR x TIL BOTH SAEM x AN ME'Z WIDTH
				VISIBLE "  " AN SUM OF x AN 1!
			IM OUTTA YR OUTR

			VISIBLE ""

			I HAS A MAXHEIGHT ITZ DIFF OF ME'Z HEIGHT AN 1

			IM IN YR OUTR UPPIN YR y TIL BOTH SAEM y AN ME'Z HEIGHT
				IM IN YR INR UPPIN YR x TIL BOTH SAEM x AN ME'Z WIDTH
					I HAS A SIMBUL
					SIMBUL R I IZ ME'Z REVILIN YR x AN YR DIFF OF MAXHEIGHT AN y MKAY
					VISIBLE "  " AN SIMBUL!
				IM OUTTA YR INR
				VISIBLE ""
			IM OUTTA YR OUTR

			VISIBLE ""

		IF U SAY SO

		HOW IZ I DROPPIN YR x
			IM IN YR CHEKIN UPPIN YR INDX TIL BOTH SAEM INDX AN ME'Z HEIGHT
				BOTH SAEM ME'Z NUN AN I IZ ME'Z GETTIN YR x AN YR INDX MKAY
				O RLY?
					YA RLY
						I IZ ME'Z SETTIN YR x AN YR INDX AN YR ME'Z TRUN MKAY
						GTFO
				OIC
			IM OUTTA YR CHEKIN
			I IZ ME'Z TRUNSWAP MKAY
		IF U SAY SO

		HOW IZ I POPPIN YR x
			I HAS A MAXHEIGHT ITZ DIFF OF ME'Z HEIGHT AN 1
			IM IN YR MOVIN UPPIN YR INDX TIL BOTH SAEM INDX AN MAXHEIGHT
				I HAS A NUVAL
				NUVAL R I IZ ME'Z GETTIN YR x AN YR SUM OF INDX AN 1 MKAY
				I IZ ME'Z SETTIN YR x AN YR INDX AN YR NUVAL MKAY
			IM OUTTA YR MOVIN
		IF U SAY SO

		HOW IZ I PLAYIN YR STR
			I HAS A INDX
			INDX R DIFF OF I IZ STRING'Z LEN YR STR MKAY AN 1
			I HAS A COL
			COL R DIFF OF MAEK I IZ STRING'Z AT YR STR AN YR INDX MKAY A NUMBR AN 1
			I IZ STRING'Z AT YR STR AN YR 0 MKAY
			WTF?
				OMG "D"
					I IZ ME'Z DROPPIN YR COL MKAY
					FOUND YR WIN
				OMG "P"
					I IZ ME'Z POPPIN YR COL MKAY
					FOUND YR WIN
				OMGWTF
					FOUND YR FAIL
			OIC
		IF U SAY SO

		HOW IZ I CONSTRUCTIN YR WIDTH AN YR HEIGHT
			ME'Z WIDTH R WIDTH
			ME'Z HEIGHT R HEIGHT
			IM IN YR CONSTRUCTION UPPIN YR COUNT TIL BOTH SAEM COUNT AN WIDTH
				IM IN YR CONSTRUCTION2 UPPIN YR COUNT2 TIL BOTH SAEM COUNT2 AN HEIGHT
					I HAS A LOC
					LOC R I IZ ME'Z PARSIN YR COUNT AN YR COUNT2 MKAY
					ME HAS A SRS LOC ITZ ME'Z NUN
				IM OUTTA YR CONSTRUCTION2
			IM OUTTA YR CONSTRUCTION
		IF U SAY SO
	KTHX

	O HAI IM KONN
		I HAS A DEBUGIN ITZ WIN
		I HAS A local
		I HAS A remote
		I HAS A M2

		HOW IZ I SENDIN YR MSG
			I HAS A FULMSG ITZ SMOOSH MSG ":(000D):)" MKAY
			I IZ SOCKS'Z PUT YR ME'Z local AN YR ME'Z remote AN YR FULMSG MKAY

			ME'Z DEBUGIN, O RLY?
				YA RLY, VISIBLE SMOOSH "SENT:: " MSG MKAY
			OIC
		IF U SAY SO

		HOW IZ I RECIVIN
			I HAS A RESPONSE
			RESPONSE R I IZ SOCKS'Z GET YR ME'Z local AN YR ME'Z remote AN YR 1024 MKAY

			I HAS A NULEN ITZ DIFF OF I IZ STRING'Z LEN YR RESPONSE MKAY AN 2
			I HAS A NURESPONSE ITZ ""

			I HAS A UERESP ITZ ""
			I HAS A W1 ITZ WIN

			IM IN YR APPENDIN UPPIN YR COUNT TIL BOTH SAEM COUNT AN NULEN
				I HAS A CHAR ITZ I IZ STRING'Z AT YR RESPONSE AN YR COUNT MKAY
				BOTH SAEM CHAR AN ":(000D)", O RLY?
					YA RLY, W1 R FAIL
					MEBBE BOTH SAEM CHAR AN ":)"
					NO WAI
						W1, O RLY?
							YA RLY, NURESPONSE R SMOOSH NURESPONSE CHAR MKAY
							NO WAI, UERESP R SMOOSH UERESP CHAR MKAY
						OIC
				OIC
			IM OUTTA YR APPENDIN

			ME'Z DEBUGIN, O RLY?
				YA RLY, VISIBLE SMOOSH "RECV:: " NURESPONSE MKAY
			OIC

			ME'Z M2 R UERESP

			FOUND YR NURESPONSE
		IF U SAY SO

		HOW IZ I SHAKIN YR NAEM
			I IZ ME'Z SENDIN YR SMOOSH "I32CFSP_HELLO " NAEM MKAY MKAY
			I IZ ME'Z RECIVIN MKAY
			I IZ ME'Z SENDIN YR "AI_GAME" MKAY
			I IZ ME'Z RECIVIN MKAY
		IF U SAY SO

		HOW IZ I OVAR YR RESP

			I HAS A LEN ITZ I IZ STRING'Z LEN YR RESP MKAY

			DIFFRINT LEN AN BIGGR OF LEN AN 7, O RLY?
				YA RLY, FOUND YR FAIL
			OIC

			I HAS A NURESP ITZ ""
			IM IN YR ITER UPPIN YR COUNT TIL BOTH SAEM COUNT AN 6
				I HAS A CHAR ITZ I IZ STRING'Z AT YR RESP AN YR COUNT MKAY
				NURESP R SMOOSH NURESP CHAR MKAY
			IM OUTTA YR ITER

			FOUND YR BOTH SAEM NURESP AN "WINNER"
		IF U SAY SO

		HOW IZ I CONSTRUCTIN
			ME'Z local R I IZ SOCKS'Z BIND YR "ANY" AN YR "ANY" MKAY
			I HAS A addr ITZ I IZ SOCKS'Z RESOLV YR "woodhouse.ics.uci.edu" MKAY

			ME'Z remote R I IZ SOCKS'Z KONN YR ME'Z local AN YR addr AN YR 4444 MKAY
		IF U SAY SO

		HOW IZ I CLOSIN
			I IZ SOCKS'Z CLOSE YR ME'Z local MKAY
		IF U SAY SO
	KTHX

	I HAS A C4BORD ITZ LIEK A BORD
	I IZ C4BORD'Z CONSTRUCTIN YR 7 AN YR 6 MKAY

	I HAS A C4KONN ITZ LIEK A KONN
	I IZ C4KONN'Z CONSTRUCTIN MKAY

	HOW IZ I NAEMCALLIN
		VISIBLE "WASS UR NAEM? "!
		I HAS A NAEM
		GIMMEH NAEM
		FOUND YR NAEM
	IF U SAY SO

	HOW IZ I RUNNIN YR bord AN YR konn

		I HAS A NAEM ITZ I IZ NAEMCALLIN MKAY

		I IZ konn'Z SHAKIN YR NAEM MKAY

		I HAS A CONT ITZ WIN
		I HAS A MOVE
		I HAS A RESP
		IM IN YR LUP UPPIN YR TMP WILE CONT
			I IZ bord'Z PRINTIN MKAY
			I HAS A VALIDMUV ITZ FAIL
			IM IN YR LUP2 UPPIN YR TMP2 WILE NOT VALIDMUV
				GIMMEH MOVE
				I IZ konn'Z SENDIN YR MOVE MKAY
				RESP R I IZ konn'Z RECIVIN MKAY

				I IZ STRING'Z AT YR RESP AN YR 0 MKAY, WTF?
					OMG "I"
						VISIBLE "INVALID MUV, TRY AGAIN"
						I IZ konn'Z RECIVIN MKAY
						GTFO
					OMG "E"
						VISIBLE "ERRUR, MUV CANNUT B FIXD!!!"
						FOUND YR FAIL
					OMGWTF
						VALIDMUV R WIN
						GTFO
				OIC
			IM OUTTA YR LUP2
			I IZ bord'Z PLAYIN YR MOVE MKAY

			I IZ konn'Z OVAR YR RESP MKAY, O RLY?
				YA RLY
					VISIBLE "WE HAV A WINNAR!!!"
					FOUND YR WIN
			OIC

			MOVE R I IZ konn'Z RECIVIN MKAY
			VISIBLE SMOOSH MOVE "PLAYIN" MKAY
			I IZ bord'Z PLAYIN YR MOVE MKAY
			I IZ bord'Z PRINTIN MKAY

			I IZ konn'Z OVAR YR konn'Z M2 MKAY, O RLY?
				YA RLY
					VISIBLE "LUKS LIK U LOSE!!!"
					FOUND YR FAIL
			OIC

			VISIBLE "EOL"

		IM OUTTA YR LUP
	IF U SAY SO

	I IZ RUNNIN YR C4BORD AN YR C4KONN MKAY

KTHXBYE
